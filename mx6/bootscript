setenv nload 0x12000000
setenv oload 0x18000000
setenv iface mmc
setenv inum 2
setenv offset 0x400
setenv bootloader bootloader

# Load the new booloader from the file system
# 1) Read a new bootloader from the mmc u-boot.imx if exist
# 2) Read the currect bootloader from the device spi flash.
# 3) Compare and update if not equal.
if load ${iface} ${inum} ${nload} ${bootloader}
then

    # Read the current booloader from the boot device
    sf probe; sf read ${oload} ${offset} ${filesize}
    # Compare the old & the new one.
    if cmp.b ${nload} ${oload} ${filesize}
    then
        echo Nothing to update
	echo Remove the sd-card and reset the board
	sleep 1000000
    else
        echo Updating the bootloader on spi flash
        sf probe ; sf erase 0 0xa0000 ; sf write ${nload} ${offset} ${filesize}
        reset
    fi

else
    echo u-boot.imx file not found
fi
