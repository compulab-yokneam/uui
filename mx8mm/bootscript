setenv nload ${loadaddr}
setenv oload ${fdt_addr}
setenv mmcdev 1
setenv mmcpart 2
setenv mmcoff 0x42
setenv bsize 0xD00
setenv bootloader bootloader

# Load the new booloader from the file system
# 1) iface/dev/part defined by the default u-boot environment
# 2) the boot media second partition must have the device bootloader
# in the partition:boot directory
if load usb 0 ${nload} ${bootloader}
then

    # Read the current booloader from the boot device
    setexpr bsize ${filesize} / 200
    setexpr bsize ${bsize} + 1
    mmc dev ${mmcdev} ${mmcpart}
    mmc read ${oload} ${mmcoff} ${bsize}

    # Compare the old & the new one.
    if cmp.b ${nload} ${oload} ${filesize}
    then
        echo Nothing to update
	echo Remove the sd-card and reset the board
	sleep 1000000
    else
        echo Updating the bootloader on mmc [${mmcdev}:${mmcpart}]
        mmc write ${nload} ${mmcoff} ${bsize}
        mmc partconf ${mmcdev} 0 ${mmcpart} 0
        reset
    fi

else
    echo imx-boot file not found
fi
